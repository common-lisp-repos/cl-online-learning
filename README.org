* Cl-Online-Learning

A collection of online learning algorithms for Linear classification written in pure Common Lisp.

** Implemented algorithms

*** Binary classifier
- Perceptron
- AROW
- SCW-I (Soft Confidence Weighted)
- Logistic Regression with SGD or ADAM optimizer

*** Multiclass classifier
- one-vs-rest ( K binary classifier required )
- one-vs-one ( K*(K-1)/2 binary classifier required )

** Installation
#+BEGIN_SRC 
cd ~/quicklisp/local-projects/
git clone https://github.com/masatoi/cl-online-learning.git
#+END_SRC
When using Roswell,
#+BEGIN_SRC 
ros install masatoi/cl-online-learning
#+END_SRC
** Usage
*** Prepare dataset
A data point is a pair of a class label (+1 or -1) and the input vector.

And dataset is represented as a sequence of data points.
READ-LIBSVM-DATA function is available to make a dataset from a sparse format used in LIBSVM (http://www.csie.ntu.edu.tw/~cjlin/libsvmtools/datasets/). This function requires the number of features of that dataset.
#+BEGIN_SRC lisp
(defparameter a1a-dim 123)
(defparameter a1a
  (clol.utils:read-libsvm-data
   (merge-pathnames #P"t/dataset/a1a" (asdf:system-source-directory :cl-online-learning))
   a1a-dim))
#+END_SRC

*** Define learner
A learner object is just a struct, therefore their constructor is available to make it.
#+BEGIN_SRC lisp
(defparameter arow-learner (clol:make-arow a1a-dim 10d0))
#+END_SRC

*** Update and Train
To update the model destructively with one data point, use the update function corresponding to the model type.
#+BEGIN_SRC lisp
(clol:arow-update arow-learner
                  (cdar a1a)  ; input
                  (caar a1a)) ; label
#+END_SRC
TRAIN function can be used to learn the dataset collectively.
#+BEGIN_SRC lisp
(clol:train arow-learner a1a)
#+END_SRC
It may be necessary to call this function several times until learning converges.

*** Predict and Test
#+BEGIN_SRC lisp
(clol:arow-predict arow-learner (cdar a1a))
; => -1.0d0

(clol:test arow-learner a1a)
; Accuracy: 84.85981%, Correct: 1362, Total: 1605
#+END_SRC

*** Multiclass classification
For multiclass data, the label of a data point is an integer representing the index of the class. 
#+BEGIN_SRC lisp
(defparameter iris-dim 4)

; A dataset in which the same label appears consecutively need to shuffle
(defparameter iris
  (clol.utils:shuffle-vector
   (coerce (clol.utils:read-libsvm-data-multiclass
            (merge-pathnames #P"t/dataset/iris.scale"
                             (asdf:system-source-directory :cl-online-learning))
            iris-dim)
	   'simple-vector)))

(defparameter iris-train (subseq iris 0 100))
(defparameter iris-test (subseq iris 100))
#+END_SRC
ONE-VS-REST and ONE-VS-ONE are available for multiclass classification by using multiple binary classifiers. In many cases, ONE-VS-ONE is more accurate, but it requires more computational resource as the number of classes increases.
#+BEGIN_SRC lisp
;; Define model
(defparameter arow-1vs1
  (clol:make-one-vs-one iris-dim      ; Input data dimension
                        3             ; Number of class
                        'arow 0.1d0)) ; Binary classifier type and its parameters

;; Train and test model
(clol:train arow-1vs1 iris-train)
(clol:test  arow-1vs1 iris-test)
; Accuracy: 98.0%, Correct: 49, Total: 50
#+END_SRC

*** Sparse data
For sparse data (most elements are 0), a data point is a pair of a class label and a instance of SPARSE-VECTOR struct, and use learners with SPARSE- prefix. READ-LIBSVM-DATA-SPARSE is available for make such a dataset.

For example, news20.binary data has too high dimensional features to handle with normal learners. However, by using the sparse version, the learner can be trained with practical computational resources.
#+BEGIN_SRC lisp
(defparameter news20.binary-dim 1355191)
(defparameter news20.binary (read-libsvm-data-sparse "/path/to/news20.binary"))

(defparameter news20.binary.arow (make-sparse-arow news20.binary-dim 10d0))
(time (loop repeat 20 do (train news20.binary.arow news20.binary)))
;; Evaluation took:
;;   1.678 seconds of real time
;;   1.678322 seconds of total run time (1.678322 user, 0.000000 system)
;;   100.00% CPU
;;   5,691,734,984 processor cycles
;;   59,933,584 bytes consed
(test news20.binary.arow news20.binary)
;; Accuracy: 99.74495%, Correct: 19945, Total: 19996
#+END_SRC

In a similar way, the sparse version learners are also available in multiclass classification.

#+BEGIN_SRC lisp
(defparameter news20-dim 62060)
(defparameter news20-train (read-libsvm-data-sparse-multiclass "/path/to/news20.scale"))
(defparameter news20-test (read-libsvm-data-sparse-multiclass "/path/to/news20.t.scale"))
(defparameter news20-arow (make-one-vs-rest news20-dim 20 'sparse-arow 10d0))
(loop repeat 12 do (train news20-arow news20-train))
(test news20-arow news20-test)
;; Accuracy: 86.90208%, Correct: 3470, Total: 3993
#+END_SRC

# *** Benchimark

** Author
Satoshi Imai (satoshi.imai@gmail.com)

** Licence
This software is released under the MIT License, see LICENSE.txt.
